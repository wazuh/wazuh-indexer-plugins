import java.util.concurrent.Callable

buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "2.19.1-SNAPSHOT")
        opensearch_no_snapshot = opensearch_version.replace("-SNAPSHOT","")
        opensearch_build = opensearch_no_snapshot + ".0"
        job_scheduler_version = System.getProperty("job_scheduler.version", opensearch_build)
        wazuh_version = System.getProperty("version", "5.0.0")
        revision = System.getProperty("revision", "0")
    }

    repositories {
        mavenLocal()
        maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.rest-test'
apply plugin: 'opensearch.pluginzip'

def pluginName = 'wazuh-indexer-command-manager'
def pluginDescription = 'The Command Manager plugin handles and distributes commands across your Wazuh environment.'
def projectPath = 'com.wazuh'
def pathToPlugin = 'commandmanager'
def pluginClassName = 'CommandManagerPlugin'

publishing {
    publications {
        pluginZip(MavenPublication) { publication ->
            pom {
                name = pluginName
                description = pluginDescription
                groupId = projectPath
                licenses {
                    license {
                        name = "GNU Affero General Public License (AGPL) version 3.0"
                        url = "http://www.gnu.org/licenses/agpl-3.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "Wazuh"
                        url = "https://wazuh.com"
                    }
                }
            }
        }
    }
}

allprojects {
    group = "${projectPath}"
    version = "${wazuh_version}" + ".${revision}"
    targetCompatibility = JavaVersion.VERSION_11
    sourceCompatibility = JavaVersion.VERSION_11
}

opensearchplugin {
    name pluginName
    description pluginDescription
    classname "${projectPath}.${pathToPlugin}.${pluginClassName}"
    extendedPlugins = ['opensearch-job-scheduler']
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
}

def versions = [
    log4j: "2.23.1"
]

configurations {
    zipArchive
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j}"

    // Job Scheduler stuff
    zipArchive group: 'org.opensearch.plugin', name: 'opensearch-job-scheduler', version: "${opensearch_build}"
    // implementation "org.opensearch:opensearch:${opensearch_version}"
    compileOnly "org.opensearch:opensearch-job-scheduler-spi:${job_scheduler_version}"

}

// No need to validate license headers since spotless checks and applies it
licenseHeaders.enabled = false

// This requires an additional Jar not published as part of build-tools
loggerUsageCheck.enabled = false

// No need to validate pom, as we do not upload to maven/sonatype
validateNebulaPom.enabled = false

// Skip forbiddenAPIs check on dependencies
thirdPartyAudit.enabled = false

//Skip checking for third party licenses
dependencyLicenses.enabled = false

repositories {
    mavenLocal()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    // imposter maven repository
    maven { url "https://s3-eu-west-1.amazonaws.com/gatehillsoftware-maven/releases/" }
}

generatePomFileForPluginZipPublication.dependsOn(publishNebulaPublicationToMavenLocal)

test {
    include '**/*Tests.class'
    jvmArgs "-Djava.security.policy=./plugins/command-manager/src/main/plugin-metadata/plugin-security.policy/plugin-security.policy"
}

def getJobSchedulerPlugin() {
    provider(new Callable<RegularFile>() {
        @Override
        RegularFile call() throws Exception {
            return new RegularFile() {
                @Override
                File getAsFile() {
                    return configurations.zipArchive.asFileTree.matching {
                        include '**/opensearch-job-scheduler*'
                    }.singleFile
                }
            }
        }
    })
}

testClusters.integTest {
    plugin(getJobSchedulerPlugin())
    testDistribution = "ARCHIVE"
    // This installs our plugin into the testClusters
    plugin(project.tasks.bundlePlugin.archiveFile)

    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}

run {
    useCluster testClusters.integTest
    exec {
        // Generate data for the wazuh-agents index, it is useful for the agent_groups commands development and testing.
        commandLine 'bash', './scripts/populate-agents-index.sh', '-n', '10', '-o', 'build/tmp/logs/'
    }
}

// updateVersion: Task to auto update version to the next development iteration
task updateVersion {
    onlyIf { System.getProperty('newVersion') }
    doLast {
        ext.newVersion = System.getProperty('newVersion')
        println "Setting version to ${newVersion}."
         // String tokenization to support -SNAPSHOT
        ant.replaceregexp(file: 'build.gradle', match: '"opensearch.version", "\\d.*"', replace: '"opensearch.version", "' + newVersion.tokenize('-')[0] + '-SNAPSHOT"', flags: 'g', byline: true)
    }
}
