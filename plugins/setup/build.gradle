import org.opensearch.gradle.test.RestIntegTestTask
import java.util.concurrent.Callable

buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.3.2-SNAPSHOT")
        opensearch_build = opensearch_version.replace("-SNAPSHOT","") + ".0"
        wazuh_version = System.getProperty("version", "5.0.0")
        revision = System.getProperty("revision", "0")
    }

    repositories {
        mavenLocal()
        maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
      classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.testclusters'
apply plugin: 'opensearch.java-rest-test'
apply plugin: 'opensearch.pluginzip'
apply plugin: 'opensearch.java-agent'

def pluginName = 'wazuh-indexer-setup'
def pluginDescription = 'Wazuh Indexer setup plugin'
def projectPath = 'com.wazuh'
def pathToPlugin = 'setup'
def pluginClassName = 'SetupPlugin'

publishing {
    publications {
        pluginZip(MavenPublication) { publication ->
            pom {
                name = pluginName
                description = pluginDescription
                licenses {
                    license {
                        name = "GNU Affero General Public License (AGPL) version 3.0"
                        url = "http://www.gnu.org/licenses/agpl-3.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "Wazuh"
                        url = "https://wazuh.com"
                    }
                }
            }
        }
    }
}

allprojects {
    group = "${projectPath}"
    version = "${wazuh_version}" + ".${revision}"
    targetCompatibility = JavaVersion.VERSION_21
    sourceCompatibility = JavaVersion.VERSION_21
}

opensearchplugin {
    name pluginName
    description pluginDescription
    classname "${projectPath}.${pathToPlugin}.${pluginClassName}"
    extendedPlugins = ['opensearch-index-management']
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
}

configurations {
    zipArchive
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.25.2"
    implementation "org.slf4j:slf4j-api:2.0.17"

    implementation 'tools.jackson.core:jackson-core:3.0.1'
    implementation 'tools.jackson.core:jackson-databind:3.0.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:3.0-rc5'

    // Job Scheduler & ISM stuff (required for the initialization of ISM policies)
    zipArchive group: 'org.opensearch.plugin', name: 'opensearch-job-scheduler', version: opensearch_build
    zipArchive group: 'org.opensearch.plugin', name: 'opensearch-index-management', version: opensearch_build
    compileOnly "org.opensearch:opensearch-job-scheduler-spi:${opensearch_build}"
}

// No need to validate license headers since spotless checks and applies it
licenseHeaders.enabled = false

// This requires an additional Jar not published as part of build-tools
loggerUsageCheck.enabled = false

// No need to validate pom, as we do not upload to maven/sonatype
validateNebulaPom.enabled = false

//Skip checking for third party licenses
dependencyLicenses.enabled = false

repositories {
    mavenLocal()
    maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

test {
    include '**/*Tests.class'
}

def getPlugin(pluginName) {
    provider(new Callable<RegularFile>() {
        @Override
        RegularFile call() throws Exception {
            return new RegularFile() {
                @Override
                File getAsFile() {
                    return configurations.zipArchive.asFileTree.matching {
                        include String.format("**/%s*", pluginName)
                    }.singleFile
                }
            }
        }
    })
}

task integTest(type: RestIntegTestTask) {
  description = "Run tests against a cluster"
  testClassesDirs = sourceSets.test.output.classesDirs
  classpath = sourceSets.test.runtimeClasspath
}
tasks.named("check").configure { dependsOn(integTest) }

integTest {
    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}


testClusters.integTest {
    plugin(getPlugin("opensearch-job-scheduler"))
    plugin(getPlugin("opensearch-index-management"))

    // Apparently, "INTEG_TEST" distribution does not contain every type parsers.
    // We need to use "ARCHIVE" to prevent the following MapperParsingException when creating index templates:
    // > Failed to parse mapping [_doc]: No handler for type [scaled_float] declared on field [usage]
    testDistribution = "ARCHIVE"
    // This installs our plugin into the testClusters
    plugin(project.tasks.bundlePlugin.archiveFile)

    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("cluster.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}


run {
    useCluster testClusters.integTest
}

// updateVersion: Task to auto update version to the next development iteration
task updateVersion {
    onlyIf { System.getProperty('newVersion') }
    doLast {
        ext.newVersion = System.getProperty('newVersion')
        println "Setting version to ${newVersion}."
        // String tokenization to support -SNAPSHOT
        ant.replaceregexp(file: 'build.gradle', match: '"opensearch.version", "\\d.*"', replace: '"opensearch.version", "' + newVersion.tokenize('-')[0] + '-SNAPSHOT"', flags: 'g', byline: true)
    }
}

